apply plugin: 'groovy'
apply plugin: 'maven'

group = 'org.gmock'
archivesBaseName = 'gmock'

repositories {
    mavenCentral()
    mavenRepo urls: ['http://repository.jboss.org/maven2/']
}

configurations {
    jarjar
    jarjarify
    compileConf
    testCompileConf
    compile.extendsFrom compileConf, jarjarify
    testCompile.extendsFrom testCompileConf
    intTestCompile.extendsFrom compileConf, testCompileConf
    intTestRuntime.extendsFrom intTestCompile
}

dependencies {
    groovy "org.codehaus.groovy:groovy-all:$groovyVersion"
    compileConf "junit:junit:$junitVersion"
    testCompileConf 'org.hamcrest:hamcrest-library:1.1',
                    'org.testng:testng:5.8:jdk15',
                    'org.apache.tomcat:servlet-api:6.0.18',
                    'dom4j:dom4j:1.6.1'
    jarjar 'com.google.code:jarjar:1.0'
    jarjarify 'cglib:cglib:2.2',
              'org.objenesis:objenesis:1.1'
    intTestCompile files(jar.archivePath)

    // Hack for maven pom generating
    compile configurations.groovy.dependencies,
            configurations.compileConf.dependencies
}

sourceSets {
    main {
        java.srcDirs = []
        groovy.srcDirs = ['src/main/java', 'src/main/groovy']
    }
    intTest {
        java.srcDirs = test.java.srcDirs
        groovy.srcDirs = test.groovy.srcDirs
        compileClasspath = configurations.intTestCompile
        runtimeClasspath = files(classes, configurations.groovy, configurations.intTestRuntime)
    }
}


// Tasks

defaultTasks ':test'

tasks.jar << {
    tmpArchivePath = file("${archivePath}.tmp")
    ant {
        move file: archivePath, tofile: tmpArchivePath
        taskdef name: 'jarjar', classname: 'com.tonicsystems.jarjar.JarJarTask', classpath: configurations.jarjar.asPath
        jarjar jarfile: archivePath, {
            zipfileset src: tmpArchivePath
            configurations.jarjarify.each {
                zipfileset src: it, {
                    include name: '**/*.class'
                }
            }
            rule pattern: "net.sf.cglib.**", result: "org.gmock.internal.cglib.@1"
            rule pattern: "org.objenesis.**", result: "org.gmock.internal.objenesis.@1"
            rule pattern: "org.objectweb.asm.**", result: "org.gmock.internal.asm.@1"
        }
        delete file: tmpArchivePath
    }
}

tasks.compileIntTestJava.dependsOn jar

task intTest(type: Test, dependsOn: intTestClasses) {
    testClassesDir = sourceSets.intTest.classesDir
    classpath = sourceSets.intTest.runtimeClasspath
    testReportDir = file("$reportsDir/int-tests")
    testResultsDir = file("$buildDir/int-test-results")
}

tasks.build.dependsOn intTest
